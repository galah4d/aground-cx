package main

// import core packages
import "gl"
import "glfw"
import "os"

// keyboard key codes
var RIGHT_ARROW_KEY i32 = 262
var LEFT_ARROW_KEY i32 = 263
var DOWN_ARROW_KEY i32 = 264
var UP_ARROW_KEY i32 = 265
var ENTER_KEY i32 = 257

var C_KEY i32 = 67
var I_KEY i32 = 73

var NUM_1_KEY i32 = 49
var NUM_9_KEY i32 = 57

/* Function     : initGLFW
   Desctription : Initializes the game window and input callback functions.
*/
func initGLFW() {
	glfw.Init()
	glfw.WindowHint(glfw.Resizable, glfw.False)
	glfw.WindowHint(glfw.ContextVersionMajor, 2)
  	glfw.WindowHint(glfw.ContextVersionMinor, 1)

	glfw.CreateWindow("window", SCREEN_WIDTH, SCREEN_HEIGHT, "Aground") // TODO add fullscreen support
	glfw.MakeContextCurrent("window")

	glfw.SetKeyCallback("window", "keyboardCallback")
	glfw.SetMouseButtonCallback("window", "mouseButtonCallback")
}

/* Function     : initOpenGL
   Output       : OpenGL program (program i32)
   Desctription : Initializes the OpenGL lib.
*/
func initGL() (program i32) {
	gl.Init()
	gl.Enable(gl.BLEND)
  	gl.BlendFunc(gl.SRC_ALPHA, gl.ONE_MINUS_SRC_ALPHA)
	gl.Enable(gl.DEPTH_TEST)
	gl.Enable(gl.TEXTURE_2D)

	program = gl.CreateProgram()
	gl.LinkProgram(program)
	return program
}


// -----------------------------------------------------------------------------

func keyboardCallback(window str, key i32, scancode i32, action i32, mods i32) () {
  // Movement keys
	if bool.and(i32.eq(key, RIGHT_ARROW_KEY), i32.eq(action, 1)) {
    g_game.player = SetPlayerDirection(g_game.player, 1, 0)

	} else if bool.and(i32.eq(key, LEFT_ARROW_KEY), i32.eq(action, 1)) {
		g_game.player = SetPlayerDirection(g_game.player, -1, 0)

	} else if bool.and(i32.eq(key, DOWN_ARROW_KEY), i32.eq(action, 1)) {
		g_game.player = SetPlayerDirection(g_game.player, 0, 1)

	} else if bool.and(i32.eq(key, UP_ARROW_KEY), i32.eq(action, 1)) {
		g_game.player = SetPlayerDirection(g_game.player, 0, -1)
  }

  if bool.and(i32.eq(key, ENTER_KEY), i32.eq(action, 1)) {
    g_game.player = PlayerHit(g_game.player)
  }

  if bool.and(i32.eq(key, C_KEY), i32.eq(action, 1)) {
	  if current_scene == CRAFTING_SCENE {
		  current_scene = GAME_SCENE
	  } else {
		  current_scene = CRAFTING_SCENE
	  }
  }

  if bool.and(i32.eq(key, I_KEY), i32.eq(action, 1)) {
	  if current_scene == INVENTORY_SCENE {
		  current_scene = GAME_SCENE
	  } else {
		  current_scene = INVENTORY_SCENE
	  }
  }

  if bool.and(bool.and(i32.gteq(key, NUM_1_KEY), i32.lteq(key, NUM_9_KEY)), i32.eq(action, 1)) {
    g_hotbar = ItemCollectionEquip(g_hotbar, key - NUM_1_KEY)
  }
}

func mouseButtonCallback(window str, key i32, action i32, mods i32) () {
	if i32.eq(action, 1) && i32.eq(key, glfw.MouseButtonLeft) {
		scenes[current_scene] = SceneClick(scenes[current_scene])
	}
}

// -----------------------------------------------------------------------------

var current_scene i32
//var current_scene Scene
var scenes[3] Scene

func main() {
  	initGLFW()
	var program i32
	program = initGL()

	// init game
  	g_game = NewGame()

  	// initializes all scenes
  	scenes[GAME_SCENE] = NewGameScene(SCREEN_WIDTH, SCREEN_HEIGHT)
	scenes[INVENTORY_SCENE] = NewInventoryScene(SCREEN_WIDTH, SCREEN_HEIGHT)
	scenes[CRAFTING_SCENE] = NewCraftingScene(SCREEN_WIDTH, SCREEN_HEIGHT)

	// init all scenes
  	//current_scene = NewGameScene(SCREEN_WIDTH, SCREEN_HEIGHT)
	current_scene = GAME_SCENE

  	// game loop
  	for bool.not(glfw.ShouldClose("window")) {
    	gl.Clear(i32.bitor(gl.COLOR_BUFFER_BIT, gl.DEPTH_BUFFER_BIT))
		gl.LoadIdentity()

		if (current_scene == GAME_SCENE) {
			g_game = GameUpdate(g_game)
	    	GameSceneRender(scenes[current_scene])
	    	GameRender(g_game, program)

		} else if (current_scene == INVENTORY_SCENE) {
			scenes[current_scene] = InventorySceneUpdate(scenes[current_scene])
			InventorySceneRender(scenes[current_scene])

		} else if (current_scene == CRAFTING_SCENE) {
			scenes[current_scene] = CraftingSceneUpdate(scenes[current_scene])
			CraftingSceneRender(scenes[current_scene])
		}


    	// handle event callbacks
    	glfw.PollEvents()
		glfw.SwapBuffers("window")
  	}
}
